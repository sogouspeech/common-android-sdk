apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    defaultConfig {
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode VERSION_CODE as int
        versionName VERSION_NAME as String
    }
    lintOptions {
        abortOnError false
    }
    buildTypes {
        debug{
            minifyEnabled false
            buildConfigField "boolean", "ENABLE_DEBUG", "true"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "ENABLE_DEBUG", "false"
        }
    }
    buildToolsVersion BUILD_TOOLS_VERSION as String
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.8.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
            task.plugins {
                grpc {
                    // Options added to --grpc_out
//                    option 'lite'
                }
            }
        }
    }
}


ext {
    grpcVersion = '1.8.0'
}

dependencies {
    implementation 'com.android.support:appcompat-v7:23.+'

    implementation 'javax.annotation:javax.annotation-api:1.2'
    implementation("io.grpc:grpc-protobuf:${grpcVersion}") {
        exclude module: 'jsr305'
    }
    implementation("io.grpc:grpc-stub:${grpcVersion}") {
        exclude module: 'jsr305'
    }
    implementation("io.grpc:grpc-auth:${grpcVersion}") {
        exclude module: 'jsr305'
    }
    implementation("io.grpc:grpc-okhttp:${grpcVersion}") {
        exclude module: 'jsr305'
    }
    // OAuth2 for Google API
    implementation('com.google.auth:google-auth-library-oauth2-http:0.9.0') {
        exclude module: 'jsr305'
        exclude module: 'httpclient'
    }
}
